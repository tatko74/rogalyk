;
; CUSTOM DISPLAY LIST DEMONSTRATION
; EXAMPLE 1
;

; KEYBOARD CODE OF LAST KEY PRESSED
BYTE CH=$02FC

; MEMORY SPACE FOR TEXT ON ADDITIONAL
; LINES
BYTE ARRAY PAGE6=$0600

; MEMORY ADDRESS POINTER  
BYTE POINTER POS

; PRINT TEXT AT SPECIFIC MEMORY ADDRESS
;
PROC PRINT_MEM(BYTE ARRAY STR, MEM)

BYTE I, J

FOR I=1 TO STR(0)
DO
 POS=MEM+I-1
 J=STR(I)&128
 STR(I)==&127

 IF STR(I)<32 THEN
   STR(I)==+64+J
 ELSEIF STR(I)<96 THEN
   STR(I)==-32+J
 FI

 POS^=STR(I)
OD

RETURN












; CUSTOM DISPLAY LIST
;
PROC DLSCREEN()

BYTE ARRAY

 ; CUSTOM DISPLAY LIST DATA
;                             1                   2                   3 
;     0   1  2 3 4 5 6  7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0  1  2 3  4  5   6
 DL=[96 112 66 0 6 0 66 0 0 2 2 2 2 2 2 2 2 2 2 7 7 2 6 6 2 2 2 2 2 2 0 66 40 6 65 32 156],


 MEM_VALUE(4) ; TEMPORARY STORAGE AREA


BYTE SDMCTL=$022F; SHADOW FOR DMA CONTROL REGISTER


BYTE TEMP; TEMPORARY STORAGE FOR DMA REGISTER


CARD SDLSTL=$230; STARTING ADDRESS OF DISPLAY LIST
		 


GRAPHICS(0); WE TAKE TEXT MODE 0 AS STARTING MODE; FOR CUSTOM DISPLAY LIST

; GET STARTING ADDRESS OF TEXT MODE 0
; DISPLAY DATA
POS=SDLSTL+4
MEM_VALUE(1)=POS^
POS=SDLSTL+5
MEM_VALUE(2)=POS^

; TEMPORARILY SHUT OFF DISPLAY
TEMP=SDMCTL
SDMCTL=0

; SET CUSTOM DISPLAY LIST
SDLSTL=DL

; UPDATE VALUES FOR CUSTOM DISPLAY LIST

; RESTORE STARTING ADDRESS OF
; TEXT MODE 0 DISPLAY DATA
POS=SDLSTL+7
POS^=MEM_VALUE(1)
POS=SDLSTL+8
POS^=MEM_VALUE(2)

; JUMP TO THE START OF CUSTOM DL
POS=SDLSTL+35
POS^=PEEK(SDLSTL)
POS=SDLSTL+36
POS^=PEEK(SDLSTL+1)

; RESTORE ORIGINAL DMA CONTROL
; REGISTER VALUE
SDMCTL=TEMP

RETURN








; MAIN CODE
;
PROC MAIN() 

BYTE I	   

; NEW DISPLAY LIST
DLSCREEN()
	 
; SET RIGHT MARGIN
POKE(82,1)

; SET COLORS
POKE(710,114)
POKE(712,118)
POKE(708,186)

;
; CUSTOM DISPLAY LIST DEMONSTRATION
;

; ADDITIONAL LINES ON TOP AND BOTTOM
; WITH SOME TEXT
SETBLOCK($0600,128,0)
PRINT_MEM(" TITLE LINE",PAGE6)
PRINT_MEM(" Press ESCAPE key to exit!",PAGE6+40)

; MISC. TEXT
FOR I=0 TO 10
DO
 PRINT("LINE ") PRINTBE(I)
OD
		
PRINTE("2 LINES OF		  TEXT MODE 2")
PRINTE("1 LINE OF TEXT MODE 0 AGAIN")
PRINTE("2 LINES OF		  TEXT MODE 1")

FOR I=16 TO 21
DO
 PRINT("LINE ") PRINTBE(I)
OD

; WAIT UNTIL ESCAPE KEY IS PRESSED
CH=255		
DO UNTIL CH=28 OD

; HOUSEKEEPING...
CH=255
GRAPHICS(0)
  
RETURN